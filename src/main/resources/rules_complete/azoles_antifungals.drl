//Last Updated 2017-01-13
package com.sample

//list any import classes here.
import function com.sample.DRLLogger.*;

import java.util.List;
import java.util.ArrayList;
import java.util.Map;
import java.util.Calendar;
import java.lang.String;

import com.sample.model.ConceptSetItem;
import com.sample.model.RHSState;

import edu.pitt.dbmi.ohdsiv5.db.ConditionEra;
import edu.pitt.dbmi.ohdsiv5.db.DrugEra;
import edu.pitt.dbmi.ohdsiv5.db.DrugExposure;
import edu.pitt.dbmi.ohdsiv5.db.DrugStrength;
import edu.pitt.dbmi.ohdsiv5.db.ExtendedDrugExposure;
import edu.pitt.dbmi.ohdsiv5.db.Measurement;
import edu.pitt.dbmi.ohdsiv5.db.Person;


/////////// 
// declare any global variables here
/////////// 
global org.hibernate.Session hibernateSession;
global java.util.Calendar currentDate;
global java.util.Calendar within48date;


/////////// 
// declare any types here
/////////// 


/////////// 
// Rules
/////////// 

rule "IMMUNOSUPPRESSANTS - AZOLES -- NO FILTER"
    when
      // Immunosuppressant ingredient drug era
      $clinDrugConcept1 : ConceptSetItem(csName == "Immunosuppressants Ingredients")
      $de1 : DrugEra(drugConceptId == $clinDrugConcept1.getConceptId()) 
      
      // Azole Antifungals ingredient drug era
      $clinDrugConcept2 : ConceptSetItem(csName == "Azole Antifungals Ingredients")
      $de2 : DrugEra(PersonId == $de1.getPersonId() && drugConceptId == $clinDrugConcept2.getConceptId())

      // Specific immunosuppressant exposure
      $clinDrugConcept3 : ConceptSetItem(csName == "Immunosuppressants") // concept set excludes opthalmic and inhaled epinephrine
      $dexp1 : ExtendedDrugExposure(personId == $de1.getPersonId() && drugExposureStartDate == $de1.getDrugEraStartDate() 
      && drugConceptId == $clinDrugConcept3.getConceptId() && ingredientConceptId == $clinDrugConcept1.getConceptId())
   
      // Specific azole antifungals exposure
      $clinDrugConcept4 : ConceptSetItem(csName == "Azole Antifungals")
      $dexp2 : ExtendedDrugExposure(personId == $de1.getPersonId() && drugExposureStartDate == $de2.getDrugEraStartDate() 
      && drugConceptId == $clinDrugConcept4.getConceptId() && ingredientConceptId == $clinDrugConcept2.getConceptId())

      $person : Person(personId == $de1.getPersonId())
    then	
      // Create a state object for the current node in the decision tree to be used by other LHS
      RHSState rhsCur = new RHSState("basic concomitant exposure of an immunosuppressant and an azole antifungal", "yes", $person);
      insertLogical(rhsCur);
    
      String s = String.format(
          "Matched drug exposures for the patient at the clinical drug level: Patient ID: %s; Immunosuppressant: %s; Azole Antifungal: %s.", 
          $de1.getPersonId(), 
          $clinDrugConcept3.getConceptId(), 
          $clinDrugConcept4.getConceptId());
      System.out.println(s);
end

rule "IMMUNOSUPPRESSANTS - AZOLES -- Fluconazole"
    when
      // First check if patient had the basic exposure
      $rhs : RHSState(stateName == "basic concomitant exposure of an immunosuppressant and an azole antifungal" && state == "yes", $person : person)
      
      // Fluconazole ingredient drug era
      $clinDrugConcept1 : ConceptSetItem(csName == "Fluconazoles Ingredients")
      $de1 : DrugEra(personId == $person.getPersonId() && drugConceptId == $clinDrugConcept1.getConceptId()) 

      // Specific fluconazole exposure
      $clinDrugConcept2 : ConceptSetItem(csName == "Fluconazoles")
      $dexp1 : ExtendedDrugExposure(personId == $de1.getPersonId() && drugExposureStartDate == $de1.getDrugEraStartDate() && drugConceptId == $clinDrugConcept2.getConceptId() && ingredientConceptId == $clinDrugConcept1.getConceptId())

    then	
      // Create a state object for the current node in the decision tree to be used by other LHS
      RHSState rhsCur = new RHSState("immunosuppressant and fluconazole", "yes", $person);
      insertLogical(rhsCur);
    
      String s = String.format(
          "Matched drug exposures for patient at the clinical drug level: Patient ID: %s; Fluconazole: %s.", // didnt include the immunosuppressant here cause it felt unnecessary
          $de1.getPersonId(),  
          $clinDrugConcept2.getConceptId());
      System.out.println(s);
end

rule "IMMUNOSUPPRESSANTS - AZOLES -- Fluconazole PO"
    when
	// First check if patient had the basic exposure
        $rhs : RHSState(stateName == "basic concomitant exposure of an immunosuppressant and an azole antifungal" && state == "yes", $person : person)	
	// Next, insure that the azole is fluconazole
	$rhs2 : RHSState(stateName == "immunosuppressant and fluconazole" && state == "yes" && person.getPersonId() == $person.getPersonId())
        // Fluconazole drug era
        $clinDrugConcept1 : ConceptSetItem(csName == "Fluconazoles Ingredients")
        $de1 : DrugEra(personId == $person.getPersonId() && drugConceptId == $clinDrugConcept1.getConceptId())        
        // Fluconazole PO drug exposure, make sure its above the threshhold
        $clinDrugConcept2 : ConceptSetItem(csName == "Fluconazoles PO")
        $dexp1 : ExtendedDrugExposure(personId == $person.getPersonId() && drugExposureStartDate == $de1.getDrugEraStartDate() && drugConceptId == $clinDrugConcept2.getConceptId() 
	  && ingredientConceptId == $clinDrugConcept1.getConceptId() && dailyDosage > 100)      
    then	
        RHSState rhsNN = new RHSState("immunosuppressant and high dose fluconazole", "yes", $rhs.getPerson());
        insertLogical(rhsNN);
        
        String s = String.format(
          "Immunosuppressant - Azole interaction for patient: %s.\n\tClinical implication: Risk of immunosuppressant toxicity.\n\tMitigating factor: Presence of an immunosuppressant and a high dose of oral fluconazole (Daily Dosage: %s mg/day, compared to the threshold value of 100 mg/day).\n\tRecommendation: Assess risk and take action if necessary.\n\tExplanation: Azole antifungals inhibit cytochrome P450 (CYP450) enzymes that metabolize immunosuppressants, leading to a potential risk of toxicity.", 
          $de1.getPersonId(), 
          $dexp1.getDailyDosage());
        System.out.println(s);
end

rule "IMMUNOSUPPRESSANTS - AZOLES -- Fluconazole IV"
    when
	// First check if patient had the basic exposure
        $rhs : RHSState(stateName == "basic concomitant exposure of an immunosuppressant and an azole antifungal" && state == "yes", $person : person)	
	// Next, insure that the azole is fluconazole
	$rhs2 : RHSState(stateName == "immunosuppressant and fluconazole" && state == "yes" && person.getPersonId() == $person.getPersonId())
        // Fluconazole drug era
        $clinDrugConcept1 : ConceptSetItem(csName == "Fluconazoles Ingredients")
        $de1 : DrugEra(personId == $person.getPersonId() && drugConceptId == $clinDrugConcept1.getConceptId())        
        // Fluconazole PO drug exposure, make sure its above the threshhold
        $clinDrugConcept2 : ConceptSetItem(csName == "Fluconazoles IV")
        $dexp1 : ExtendedDrugExposure(personId == $person.getPersonId() && drugExposureStartDate == $de1.getDrugEraStartDate() && drugConceptId == $clinDrugConcept2.getConceptId() 
	  && ingredientConceptId == $clinDrugConcept1.getConceptId() && dailyDosage > 200)      
    then	
        RHSState rhsNN = new RHSState("immunosuppressant and high dose fluconazole", "yes", $rhs.getPerson());
        insertLogical(rhsNN);
        
        String s = String.format(
          "Immunosuppressant - Azole interaction for patient: %s.\n\tClinical implication: Risk of immunosuppressant toxicity.\n\tMitigating factor: Presence of an immunosuppressant and a high dose of intravenous fluconazole (Daily Dosage: %s mg/day, compared to the threshold value of 200 mg/day).\n\tRecommendation: Assess risk and take action if necessary.\n\tExplanation: Azole antifungals inhibit cytochrome P450 (CYP450) enzymes that metabolize immunosuppressants, leading to a potential risk of toxicity.", 
          $de1.getPersonId(), 
          $dexp1.getDailyDosage());
        System.out.println(s);
end

rule "IMMUNOSUPPRESSANTS - AZOLES -- No Fluconazole"
    when
      // First check if patient had the basic exposure
      $rhs : RHSState(stateName == "basic concomitant exposure of an immunosuppressant and an azole antifungal" && state == "yes", $person : person)
      
      // Insure that the patient was not prescribed fluconazole
      not ($rhs2 : RHSState(stateName == "immunosuppressant and fluconazole" && state == "yes" && person.getPersonId() == $person.getPersonId()))      
      
      // Azole Antifungals ingredient drug era
      $clinDrugConcept1 : ConceptSetItem(csName == "Azole Antifungals Ingredients")
      $de1 : DrugEra(PersonId == $person.getPersonId() && drugConceptId == $clinDrugConcept1.getConceptId())
      
       // Specific azole antifungals exposure
      $clinDrugConcept2 : ConceptSetItem(csName == "Azole Antifungals")
      $dexp1 : ExtendedDrugExposure(personId == $de1.getPersonId() && drugExposureStartDate == $de1.getDrugEraStartDate() 
      && drugConceptId == $clinDrugConcept2.getConceptId() && ingredientConceptId == $clinDrugConcept1.getConceptId())     

    then	
        RHSState rhsNN = new RHSState("immunosuppressant and non-fluconazole antifungal", "yes", $rhs.getPerson());
        insertLogical(rhsNN);
        
        String s = String.format(
          "Immunosuppressant - Azole interaction for patient: %s.\n\tClinical implication: Risk of immunosuppressant toxicity.\n\tMitigating factor: Presence of an immunosuppressant and a non-fluconazole azole (%s).\n\tRecommendation: Assess risk and take action if necessary.\n\tExplanation: Azole antifungals inhibit cytochrome P450 (CYP450) enzymes that metabolize immunosuppressants, leading to a potential risk of toxicity.", 
          $de1.getPersonId(), 
          $dexp1.getDrugConceptId());
        System.out.println(s);
end