//Last Updated 2017-01-13
package com.sample

//list any import classes here.
import function com.sample.DRLLogger.*;

import java.util.List;
import java.util.ArrayList;
import java.util.Map;
import java.util.Calendar;
import java.lang.String;

import com.sample.model.ConceptSetItem;
import com.sample.model.RHSState;

import edu.pitt.dbmi.ohdsiv5.db.ConditionEra;
import edu.pitt.dbmi.ohdsiv5.db.DrugEra;
import edu.pitt.dbmi.ohdsiv5.db.DrugExposure;
import edu.pitt.dbmi.ohdsiv5.db.DrugStrength;
import edu.pitt.dbmi.ohdsiv5.db.ExtendedDrugExposure;
import edu.pitt.dbmi.ohdsiv5.db.Measurement;
import edu.pitt.dbmi.ohdsiv5.db.Person;


/////////// 
// declare any global variables here
/////////// 
global org.hibernate.Session hibernateSession;
global java.util.Calendar currentDate;
global java.util.Calendar within48date;


/////////// 
// declare any types here
/////////// 


/////////// 
// Rules
/////////// 

rule "FLUCONAZOLE - OPIOIDS -- BASIC INTERACTION AND DOSAGE CHECK"
    when
      // Fluconazole ingredient drug era
      $clinDrugConcept1 : ConceptSetItem(csName == "Fluconazoles Ingredients")
      $de1 : DrugEra(drugConceptId == $clinDrugConcept1.getConceptId()) 
      
      // Opioids ingredient drug era
      $clinDrugConcept2 : ConceptSetItem(csName == "Opioids Ingredients")
      $de2 : DrugEra(PersonId == $de1.getPersonId() && drugConceptId == $clinDrugConcept2.getConceptId())

      // Specific fluconazole exposure
      $clinDrugConcept3 : ConceptSetItem(csName == "Fluconazoles")
      $dexp1 : ExtendedDrugExposure(personId == $de1.getPersonId() && drugExposureStartDate == $de1.getDrugEraStartDate() 
      && drugConceptId == $clinDrugConcept3.getConceptId() && ingredientConceptId == $clinDrugConcept1.getConceptId() && dailyDosage > 50.0)
   
      // Specific opioids exposure
      $clinDrugConcept4 : ConceptSetItem(csName == "Opioids")
      $dexp2 : ExtendedDrugExposure(personId == $de1.getPersonId() && drugExposureStartDate == $de2.getDrugEraStartDate() 
      && drugConceptId == $clinDrugConcept4.getConceptId() && ingredientConceptId == $clinDrugConcept2.getConceptId())

      $person : Person(personId == $de1.getPersonId())
    then	
      RHSState rhsCur = new RHSState("basic concomitant exposure of a high dose of fluconazole and an opioid", "yes", $person);
      insertLogical(rhsCur);
    
      String s = String.format(
          "Matched drug exposures for the patient at the clinical drug level: Patient ID: %s; Fluconazole: %s (Daily Dosage: %s mg/day, Threshold Value: 50.0 mg/day); Opioid: %s.", 
          $de1.getPersonId(), 
          $clinDrugConcept3.getConceptId(),
	  $dexp1.getDailyDosage(),
          $clinDrugConcept4.getConceptId());
      System.out.println(s);
end

rule "FLUCONAZOLE - OPIOIDS -- FENTANYL"
    when
        $rhs : RHSState(stateName == "basic concomitant exposure of a high dose of fluconazole and an opioid" && state == "yes")
        
        // PPI or Misoprostol drug era
        $clinDrugConcept1 : ConceptSetItem(csName == "Fentanyls Ingredients")
        $de1 : DrugEra(personId == $rhs.getPerson().getPersonId() && drugConceptId == $clinDrugConcept1.getConceptId())
                                      
        // Specific PPI or Misoprostol exposure 
        $clinDrugConcept2 : ConceptSetItem(csName == "Fentanyls")
        $dexp1 : ExtendedDrugExposure(personId == $rhs.getPerson().getPersonId() && drugExposureStartDate == $de1.getDrugEraStartDate() 
	&& drugConceptId == $clinDrugConcept2.getConceptId() && ingredientConceptId == $clinDrugConcept1.getConceptId())                
    then	
        RHSState rhsNN = new RHSState("high dose fluconazole and fentanyl", "yes", $rhs.getPerson());
        insertLogical(rhsNN);
        
        String s = String.format(
          "Fluconazole - Fentanyl interaction for patient: %s.\n\tClinical implication: Possible increased risk of opioid toxicity\n\tMitigating factor: Co-prescription of fluconazole and fentanyl (%s).\n\tRecommendation: Assess risk and take action if necessary.\n\tExplanation:  Fluconazole inhibits CYP3A4, which may cause an increase in opioid plasma concentration.", 
          $de1.getPersonId(), 
          $dexp1.getDrugConceptId());
        System.out.println(s);
end

rule "FLUCONAZOLE - OPIOIDS -- OXYCODONE"
    when
        $rhs : RHSState(stateName == "basic concomitant exposure of a high dose of fluconazole and an opioid" && state == "yes")
        
        // Fentanyl drug era
        $clinDrugConcept1 : ConceptSetItem(csName == "Oxycodones Ingredients")
        $de1 : DrugEra(personId == $rhs.getPerson().getPersonId() && drugConceptId == $clinDrugConcept1.getConceptId())
                                      
        // Specific fentanyl exposure 
        $clinDrugConcept2 : ConceptSetItem(csName == "Oxycodones")
        $dexp1 : ExtendedDrugExposure(personId == $rhs.getPerson().getPersonId() && drugExposureStartDate == $de1.getDrugEraStartDate() 
	&& drugConceptId == $clinDrugConcept2.getConceptId() && ingredientConceptId == $clinDrugConcept1.getConceptId() && dailyDosage > 20.0)                
    then	
        RHSState rhsNN = new RHSState("high dose fluconazole and oxycodone", "yes", $rhs.getPerson());
        insertLogical(rhsNN);
        
        String s = String.format(
          "Fluconazole - Oxycodone interaction for patient: %s.\n\tClinical implication: Possible increased risk of opioid toxicity\n\tMitigating factor: Co-prescription of fluconazole and a high dose of oxycodone (Daily Dosage: %s mg/day, Threshold Value 20.0 mg/day).\n\tRecommendation: Assess risk and take action if necessary.\n\tExplanation:  Fluconazole inhibits CYP3A4, which may cause an increase in opioid plasma concentration.", 
          $de1.getPersonId(), 
          $dexp1.getDailyDosage());
        System.out.println(s);
end