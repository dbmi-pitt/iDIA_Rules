// Clonidine - Beta-Blockers DDI Alerting Rule
// Written By: Sam Rosko
// Last Update: 2017-04-10
// Status: Completed
// Expected Output: 17 alerts for 17 patients
// KNOWN ISSUE: Esmolol rule is a placeholder, cannot be implemented in OHDSI model due to nature of data and rule requirements (need IV dispensing data)

package com.sample

//list any import classes here.
import function com.sample.DRLLogger.*;

import java.util.*;
import java.lang.String;
import java.sql.Timestamp;

import com.sample.model.ConceptSetItem;
import com.sample.model.RHSState;

import edu.pitt.dbmi.ohdsiv5.db.ConditionEra;
import edu.pitt.dbmi.ohdsiv5.db.DrugEra;
import edu.pitt.dbmi.ohdsiv5.db.DrugExposure;
import edu.pitt.dbmi.ohdsiv5.db.DrugStrength;
import edu.pitt.dbmi.ohdsiv5.db.ExtendedDrugExposure;
import edu.pitt.dbmi.ohdsiv5.db.Measurement;
import edu.pitt.dbmi.ohdsiv5.db.Person;


/////////// 
// declare any global variables here
/////////// 
global java.lang.String currentDateStr;
global java.sql.Timestamp currentDate;
global java.sql.Timestamp within48hours;
global java.sql.Timestamp within28days;
global java.sql.Timestamp plus1day;


/////////// 
// declare any types here
/////////// 


/////////// 
// Rules
/////////// 

// The first rule is a basic check to identify all patients on any dosage or form of clonidine and a beta-blocker at the same time
rule "CLONIDINE - BETA-BLOCKERS -- NO FILTER"
    when
      // First, we look for patients exposed to clonidine on the ingredient level
      $clinDrugConcept1 : ConceptSetItem(csName == "Clonidines Ingredients")
      $de1 : DrugEra(drugConceptId == $clinDrugConcept1.getConceptId()) 
      // Second, we similarly look for exposure to a beta-blocker on the ingredient level
      $clinDrugConcept2 : ConceptSetItem(csName == "Beta-Blockers Ingredients")
      $de2 : DrugEra(PersonId == $de1.getPersonId() && drugConceptId == $clinDrugConcept2.getConceptId())
      // Next, we identify the specific drug exposures for both clonidine and the beta-blocker
      $clinDrugConcept3 : ConceptSetItem(csName == "Clonidines") 
      $dexp1 : ExtendedDrugExposure(personId != null && personId == $de1.getPersonId()
          && drugExposureStartDate != null && drugExposureStartDate >= currentDate && drugExposureStartDate < plus1day
          && drugConceptId == $clinDrugConcept3.getConceptId() && ingredientConceptId == $clinDrugConcept1.getConceptId())
      $clinDrugConcept4 : ConceptSetItem(csName == "Beta-Blockers")
      // ((de2.drug_exposure_start_date <= de1.drug_exposure_end_date AND de2.drug_exposure_start_date >= de1.drug_exposure_start_date) OR (de1.drug_exposure_start_date <= de2.drug_exposure_end_date AND de1.drug_exposure_start_date >= de2.drug_exposure_start_date))
      $dexp2 : ExtendedDrugExposure(personId != null && personId == $de1.getPersonId()
          && ((drugExposureStartDate >= $dexp1.getDrugExposureStartDate() && drugExposureStartDate <= $dexp1.getDrugExposureEndDate())
          || (drugExposureEndDate >= $dexp1.getDrugExposureStartDate() && drugExposureEndDate <= $dexp1.getDrugExposureEndDate()))
          && drugConceptId != null && drugConceptId == $clinDrugConcept4.getConceptId() 
          && ingredientConceptId != null && ingredientConceptId == $clinDrugConcept2.getConceptId())
      // This creates the patient as an object that can be used in the RHS state in the right hand side of the rule
      $person : Person(personId == $de1.getPersonId())
    then  
      // This creates a new "state" for any patients who are found to be on both drugs...
      // This state can be used as an input on further rules to quickly filter to only those patients who have made it through previous rules
      RHSState rhsCur = new RHSState("basic concomitant exposure of clonidine and a beta-blocker", "yes", $person);
      insertLogical(rhsCur);
      // The output string reports simply that a potential interaction was identified through concurrent drug exposures and lists the drug IDs
      String s = String.format(
          "Matched drug exposures for the patient at the clinical drug level: Patient ID: %s; Clonidine: %s -- dexpid %s: start %s -- end %s; Beta-Blocker: %s -- dexpid %s: start %s -- end %s;", 
          $de1.getPersonId(), 
          $clinDrugConcept3.getConceptId(), 
          $dexp1.getDrugExposureId(), $dexp1.getDrugExposureStartDate(), $dexp1.getDrugExposureEndDate(),
          $clinDrugConcept4.getConceptId(),
          $dexp2.getDrugExposureId(), $dexp2.getDrugExposureStartDate(), $dexp2.getDrugExposureEndDate());
      System.out.println(s);
      s = String.format("DATA\t%s\t%s\tclonidine and a beta-blocker interaction\tbasic concomitant exposure", currentDateStr, $person.getPersonId());
     System.out.println(s);
end

/*Carteolol
Levobunolol
Nadolol
Penbutolol
Pindolol
Propranolol
Sotalol*/
// The second rule is the first of 2 rules for all non-cardioselective beta-blockers
// Non-selective beta-blockers can yield 2 different alerts depending on cclonidine formulation
rule "CLONIDINE - BETA-BLOCKERS -- Clonidine Patch, Non-Selective Beta-Blockers"
    when
        // it doesn't appear that the "Beta-Blockers" concept set overlaps with the 7 non-selective beta-blockers listed. No RHS used for now.
        // $rhs : RHSState(stateName == "basic concomitant exposure of clonidine and a beta-blocker" && state == "yes", $person : person)
        // Next, we check for patient exposure to a non-selective beta blocker on the ingredient level
  // This set includes Timolol, even though it has its own rule as its different dose forms fall into different areas
        // $clinDrugConcept1 : ConceptSetItem(csName == "Non-Selective Beta-Blockers Ingredients")
        $clinDrugConcept1 : ConceptSetItem(csName == "Carteolols Ingredients" || csName == "Levobunolols Ingredients" || csName == "Nadolols Ingredients" || csName == "Penbutolols Ingredients" || csName == "Pindolols Ingredients" || csName == "Propranolols Ingredients" || csName == "Sotalols Ingredients")
        $de1 : DrugEra(drugConceptId == $clinDrugConcept1.getConceptId())        
        // And following that, we check the patient exposure on the clinical drug level
  // This includes Timolol oral, eliminating the need for a separate rule
        $clinDrugConcept2 : ConceptSetItem(csName == "Carteolols" || csName == "Levobunolols" || csName == "Nadolols" || csName == "Penbutolols" || csName == "Pindolols" || csName == "Propranolols" || csName == "Sotalols")
        $dexp1 : ExtendedDrugExposure(personId != null && personId == $de1.getPersonId()
            && drugExposureStartDate != null && drugExposureStartDate >= currentDate && drugExposureStartDate < plus1day
            && drugConceptId != null && drugConceptId == $clinDrugConcept2.getConceptId()
            && ingredientConceptId != null && ingredientConceptId == $clinDrugConcept1.getConceptId())

        $clinDrugConcept3 : ConceptSetItem(csName == "Clonidines Ingredients")
        $de2 : DrugEra(personId == $de1.getPersonId() && drugConceptId == $clinDrugConcept3.getConceptId())        
        $clinDrugConcept4 : ConceptSetItem(csName == "Clonidines Topical")
        $dexp2 : ExtendedDrugExposure(personId != null && personId == $de1.getPersonId()
          && ((drugExposureStartDate >= $dexp1.getDrugExposureStartDate() && drugExposureStartDate <= $dexp1.getDrugExposureEndDate())
          || (drugExposureEndDate >= $dexp1.getDrugExposureStartDate() && drugExposureEndDate <= $dexp1.getDrugExposureEndDate()))
          && drugConceptId != null && drugConceptId == $clinDrugConcept4.getConceptId() 
          && ingredientConceptId != null && ingredientConceptId == $clinDrugConcept3.getConceptId())

        $person : Person(personId == $de1.getPersonId());
    then
        RHSState rhsNN = new RHSState("clonidine, patch yes, non-selective beta-blocker", "yes", $person);
        insertLogical(rhsNN);
        // This outputs the recommendations and rationale for the patient because this is a leaf node       
        String s = String.format(
          "Clonidine - Beta-blocker interaction for patient: %s.\n\tClinical implication: Rebound hypertension possible on discontinuation of clonidine\n\tRecommendation: Assess risk and take action if necessary\n\tMitigating factor: (TODO)\n\tExplanation: Non-selective beta-blocker = %s, Clonidine Topical = %s",
          $de1.getPersonId(), 
          $dexp1.getDrugConceptId(),
          $dexp2.getDrugConceptId());
        System.out.println(s);
        s = String.format("DATA\t%s\t%s\tclonidine and a beta-blocker interaction\tclonidine, patch yes, non-selective beta-blocker", currentDateStr, $person.getPersonId());
        System.out.println(s);
end

// Second of 2 rules for all non-cardioselective beta-blockers
// Non-selective beta-blockers can yield 2 different alerts depending on cclonidine formulation
rule "CLONIDINE - BETA-BLOCKERS -- Clonidine (Oral or Epidural), Non-Selective Beta-Blockers" 
    when
        // it doesn't appear that the "Beta-Blockers" concept set overlaps with the 7 non-selective beta-blockers. No RHS used for now.
        // $rhs : RHSState(stateName == "clonidine and non-selective beta-blockers" && state == "yes", $person : person)
        $clinDrugConcept1 : ConceptSetItem(csName == "Carteolols Ingredients" || csName == "Levobunolols Ingredients" || csName == "Nadolols Ingredients" || csName == "Penbutolols Ingredients" || csName == "Pindolols Ingredients" || csName == "Propranolols Ingredients" || csName == "Sotalols Ingredients")
        $de1 : DrugEra(drugConceptId == $clinDrugConcept1.getConceptId())        
        // And following that, we check the patient exposure on the clinical drug level
  // This includes Timolol oral, eliminating the need for a separate rule
        $clinDrugConcept2 : ConceptSetItem(csName == "Carteolols" || csName == "Levobunolols" || csName == "Nadolols" || csName == "Penbutolols" || csName == "Pindolols" || csName == "Propranolols" || csName == "Sotalols")
        $dexp1 : ExtendedDrugExposure(personId != null && personId == $de1.getPersonId()
            && drugExposureStartDate != null && drugExposureStartDate >= currentDate && drugExposureStartDate < plus1day
            && drugConceptId != null && drugConceptId == $clinDrugConcept2.getConceptId()
            && ingredientConceptId != null && ingredientConceptId == $clinDrugConcept1.getConceptId()) 

        $clinDrugConcept3 : ConceptSetItem(csName == "Clonidines Ingredients")
        $de2 : DrugEra(personId == $de1.getPersonId() && drugConceptId == $clinDrugConcept3.getConceptId())        
        $clinDrugConcept4 : ConceptSetItem(csName == "Clonidines Oral" || csName == "Clonidines Injectable") // NOTE: can Injectable be used for "epidural"?
        $dexp2 : ExtendedDrugExposure(personId != null && personId == $de1.getPersonId()
          && ((drugExposureStartDate >= $dexp1.getDrugExposureStartDate() && drugExposureStartDate <= $dexp1.getDrugExposureEndDate())
          || (drugExposureEndDate >= $dexp1.getDrugExposureStartDate() && drugExposureEndDate <= $dexp1.getDrugExposureEndDate()))
          && drugConceptId != null && drugConceptId == $clinDrugConcept4.getConceptId() 
          && ingredientConceptId != null && ingredientConceptId == $clinDrugConcept3.getConceptId())

        $person : Person(personId == $de1.getPersonId());
    then
        RHSState rhsNN = new RHSState("clonidine, oral or epidural yes, non-selective beta-blocker", "yes", $person);
        insertLogical(rhsNN);
        // This outputs the recommendations and rationale for the patient because this is a leaf node       
        String s = String.format(
          "Clonidine - Beta-blocker interaction for patient: %s.\n\tClinical implication: Rebound hypertension likely on discontinuation of clonidine\n\tRecommendation: Use only if benefit outweighs risk\n\tMitigating factor: (TODO)\n\tExplanation: Non-selective beta-blocker = %s, Clonidine Oral or Epidural = %s",
          $de1.getPersonId(), 
          $dexp1.getDrugConceptId(),
          $dexp2.getDrugConceptId());
        System.out.println(s);
        s = String.format("DATA\t%s\t%s\tclonidine and a beta-blocker interaction\tclonidine, oral or epidural yes, non-selective beta-blocker", currentDateStr, $person.getPersonId());
        System.out.println(s);
end

// The third rule is the first of a set of three about Timolol
// This rule checks to see if the patients are on Timolol Eye Drops and the clondine formulation is a patch (topical/transdermal)
rule "CLONIDINE - BETA-BLOCKERS -- Clonidine (Patch), Timolol (Eye Drops)"
    when
        // This checks the state of the patients to identify only those who were identified to be on both medications of interest
        $rhs : RHSState(stateName == "basic concomitant exposure of clonidine and a beta-blocker" && state == "yes", $person : person)
        // Next, we bring in the patient's Timolol exposure on the ingredient level and the clinical drug level (Eye Drops only b/c they're the only form that have this effect)
        $clinDrugConcept1 : ConceptSetItem(csName == "Timolols Ingredients")
        $de1 : DrugEra(personId == $person.getPersonId() && drugConceptId == $clinDrugConcept1.getConceptId())        
        $clinDrugConcept2 : ConceptSetItem(csName == "Timolols Eye Drops")
        $dexp1 : ExtendedDrugExposure(personId != null && personId == $de1.getPersonId()
          && drugExposureStartDate != null && drugExposureStartDate <= currentDate && drugExposureEndDate >= currentDate
          && drugConceptId != null && drugConceptId == $clinDrugConcept2.getConceptId() 
          // && routeConceptId != null && routeConceptId == $clinRouteConcept1.getConceptId()
          && ingredientConceptId != null && ingredientConceptId == $clinDrugConcept1.getConceptId())

        $clinDrugConcept3 : ConceptSetItem(csName == "Clonidines Ingredients")
        $de2 : DrugEra(personId == $person.getPersonId() && drugConceptId == $clinDrugConcept3.getConceptId())        
        $clinDrugConcept4 : ConceptSetItem(csName == "Clonidines Topical")
        $dexp2 : ExtendedDrugExposure(personId != null && personId == $de1.getPersonId()
          && ((drugExposureStartDate >= $dexp1.getDrugExposureStartDate() && drugExposureStartDate <= $dexp1.getDrugExposureEndDate())
          || (drugExposureEndDate >= $dexp1.getDrugExposureStartDate() && drugExposureEndDate <= $dexp1.getDrugExposureEndDate()))
          && drugConceptId != null && drugConceptId == $clinDrugConcept4.getConceptId() 
          && ingredientConceptId != null && ingredientConceptId == $clinDrugConcept3.getConceptId())
    then
  // This creates a new "state" for any patients who are found to be on timolol and a cyp2d6 poor metabolizers
        RHSState rhsNN = new RHSState("clonidine, patch yes, timolol, eye drops yes", "yes", $rhs.getPerson());
        insertLogical(rhsNN);
        // This outputs the recommendations and rationale for the patient because this is a leaf node       
        String s = String.format(
          "Clonidine - Beta-blocker interaction for patient: %s.\n\tClinical implication: Rebound hypertension possible on discontinuation of clonidine\n\tRecommendation: Assess risk and take action if necessary\n\tMitigating factor: Timolol eye drops given.\n\tExplanation: Timolol Eye Drop = %s, Clonidine Topical = %s", 
          $de1.getPersonId(), 
          $dexp1.getDrugConceptId(),
          $dexp2.getDrugConceptId());
        System.out.println(s);
        s = String.format("DATA\t%s\t%s\tclonidine and a beta-blocker interaction\tclonidine, patch yes, timolol, eye drops yes", currentDateStr, $person.getPersonId());
        System.out.println(s);
end

// The third rule is the second of a set of three about Timolol
// This rule checks to see if the patients are on Timolol Eye Drops and the clondine formulation is oral or epidural
rule "CLONIDINE - BETA-BLOCKERS -- Clonidine (Oral or Epidural), Timolol (Eye Drops)"
    when
  // This checks the state of the patients to identify only those who were identified to be on both medications of interest
        $rhs : RHSState(stateName == "basic concomitant exposure of clonidine and a beta-blocker" && state == "yes", $person : person)
  // Next, we bring in the patient's Timolol exposure on the ingredient level and the clinical drug level (Eye Drops only b/c they're the only form that have this effect)
        $clinDrugConcept1 : ConceptSetItem(csName == "Timolols Ingredients")
        $de1 : DrugEra(personId == $person.getPersonId() && drugConceptId == $clinDrugConcept1.getConceptId())        
        $clinDrugConcept2 : ConceptSetItem(csName == "Timolols Eye Drops")
        $dexp1 : ExtendedDrugExposure(personId != null && personId == $de1.getPersonId()
          && drugExposureStartDate != null && drugExposureStartDate <= currentDate && drugExposureEndDate >= currentDate
          && drugConceptId != null && drugConceptId == $clinDrugConcept2.getConceptId() 
          // && routeConceptId != null && routeConceptId == $clinRouteConcept1.getConceptId()
          && ingredientConceptId != null && ingredientConceptId == $clinDrugConcept1.getConceptId())

        $clinDrugConcept3 : ConceptSetItem(csName == "Clonidines Ingredients")
        $de2 : DrugEra(personId == $person.getPersonId() && drugConceptId == $clinDrugConcept3.getConceptId())        
        $clinDrugConcept4 : ConceptSetItem(csName == "Clonidines Oral" || csName == "Clonidines Injectable") // NOTE: can Injectable be used for "epidural"?
        $dexp2 : ExtendedDrugExposure(personId != null && personId == $de1.getPersonId()
          && ((drugExposureStartDate >= $dexp1.getDrugExposureStartDate() && drugExposureStartDate <= $dexp1.getDrugExposureEndDate())
          || (drugExposureEndDate >= $dexp1.getDrugExposureStartDate() && drugExposureEndDate <= $dexp1.getDrugExposureEndDate()))
          && drugConceptId != null && drugConceptId == $clinDrugConcept4.getConceptId() 
          && ingredientConceptId != null && ingredientConceptId == $clinDrugConcept3.getConceptId())
    then
  // This creates a new "state" for any patients who are found to be on timolol and a cyp2d6 poor metabolizers
        RHSState rhsNN = new RHSState("clonidine, oral or epidural yes, timolol, eye drops yes", "yes", $rhs.getPerson());
        insertLogical(rhsNN);
        // This outputs the recommendations and rationale for the patient because this is a leaf node       
        String s = String.format(
          "Clonidine - Beta-blocker interaction for patient: %s.\n\tClinical implication: Rebound hypertension likely on discontinuation of clonidine\n\tRecommendation: Use only if benefit outweighs risk\n\tMitigating factor: Timolol eye drops given.\n\tExplanation: Timolol Eye Drop = %s, Clonidine Oral or Epidural = %s",
          $de1.getPersonId(), 
          $dexp1.getDrugConceptId(),
          $dexp2.getDrugConceptId());
        System.out.println(s);
        s = String.format("DATA\t%s\t%s\tclonidine and a beta-blocker interaction\tclonidine, oral or epidural yes, timolol, eye drops yes", currentDateStr, $person.getPersonId());
        System.out.println(s);
end

// The third rule is the third of a set of three about Timolol
// This rule checks to see if the patients are on Timolol Eye Gels
rule "CLONIDINE - BETA-BLOCKERS -- Timolol (Eye Gels)"
    when
  // This checks the state of the patients to identify only those who were identified to be on both medications of interest
        $rhs : RHSState(stateName == "basic concomitant exposure of clonidine and a beta-blocker" && state == "yes", $person : person)
        not ($rhs2 : RHSState(stateName == "clonidine, patch yes, timolol, eye drops yes" && state == "yes" && person.getPersonId() == $person.getPersonId()))
  // Next, we bring in the patient's Timolol exposure on the ingredient level and the clinical drug level (Eye Drops only b/c they're the only form that have this effect)
        $clinDrugConcept1 : ConceptSetItem(csName == "Timolols Ingredients")
        $de1 : DrugEra(personId == $person.getPersonId() && drugConceptId == $clinDrugConcept1.getConceptId())        
        $clinDrugConcept2 : ConceptSetItem(csName == "Timolols Eye Gels")
        $dexp1 : ExtendedDrugExposure(personId != null && personId == $de1.getPersonId()
          && drugExposureStartDate != null && drugExposureStartDate <= currentDate && drugExposureEndDate >= currentDate
          && drugConceptId != null && drugConceptId == $clinDrugConcept2.getConceptId() 
          // && routeConceptId != null && routeConceptId == $clinRouteConcept1.getConceptId()
          && ingredientConceptId != null && ingredientConceptId == $clinDrugConcept1.getConceptId())
    then
  // This creates a new "state" for any patients who are found to be on timolol and a cyp2d6 poor metabolizers
        RHSState rhsNN = new RHSState("clonidine, timolol, eye gels yes", "yes", $rhs.getPerson());
        insertLogical(rhsNN);
        // This outputs the recommendations and rationale for the patient because this is a leaf node       
        String s = String.format(
          "Clonidine - Beta-blocker interaction for patient: %s.\n\tClinical implication: Rebound hypertension unlikely on discontinuation of clonidine\n\tRecommendation: No special precautions\n\tMitigating factor: Timolol eye drops given.\n\tExplanation: Timolol Eye Gel = %s", 
          $de1.getPersonId(), 
          $dexp1.getDrugConceptId());
        System.out.println(s);
        s = String.format("DATA\t%s\t%s\tclonidine and a beta-blocker interaction\tclonidine, timolol, eye gels yes", currentDateStr, $person.getPersonId());
        System.out.println(s);
end

// The fourth rule is the third of a set of three about Timolol
// This rule checks to see if patients are on Timolol Systemic
// (TODO - "Timolols Systemic" concept set?)
/*
rule "CLONIDINE - BETA-BLOCKERS -- Timolol (Systemic)"
    when
  // This checks the state of the patients to identify only those who were identified to be on both medications of interest
        $rhs : RHSState(stateName == "basic concomitant exposure of clonidine and a beta-blocker" && state == "yes", $person : person)
        not ($rhs2 : RHSState(stateName == "clonidine, patch yes, timolol, eye drops yes" && state == "yes" && person.getPersonId() == $person.getPersonId()))
        not ($rhs3 : RHSState(stateName == "clonidine, timolol, eye gels yes" && state == "yes" && person.getPersonId() == $person.getPersonId()))
  // This makes sure that the patients being run through this rule are not CYP2D6 poor metabolizers
  not ($rhs2 : RHSState(stateName == "clonidine, timolol, and cyp2d6 PM" && state == "yes" && person.getPersonId() == $person.getPersonId()))
  // Next, we bring in the patient's Timolol exposure on the ingredient level and the clinical drug level (Eye Drops only b/c they're the only form that have this effect)
        $clinDrugConcept1 : ConceptSetItem(csName == "Timolols Ingredients")
        $de1 : DrugEra(personId == $person.getPersonId() && drugConceptId == $clinDrugConcept1.getConceptId())        
        $clinDrugConcept2 : ConceptSetItem(csName == "Timolols Systemic")
        $dexp1 : ExtendedDrugExposure(personId != null && personId == $de1.getPersonId()
          && drugExposureStartDate != null && drugExposureStartDate <= currentDate && drugExposureEndDate >= currentDate
          && drugConceptId != null && drugConceptId == $clinDrugConcept2.getConceptId() 
          // && routeConceptId != null && routeConceptId == $clinRouteConcept1.getConceptId()
          && ingredientConceptId != null && ingredientConceptId == $clinDrugConcept1.getConceptId())     
    then
  // This creates a new "state" for any patients who are found to be on timolol and a cyp2d6 inhibitor
        RHSState rhsNN = new RHSState("clonidine, timolol, systemic yes", "yes", $rhs.getPerson());
        insertLogical(rhsNN);
        // This outputs the recommendations and rationale for the patient because this is a leaf node
        String s = String.format(
          "Clonidine - Beta-blocker interaction for patient: %s.\n\tClinical implication: (TODO)\n\tMitigating factor: Timolol systemic given.\n\tRecommendation: Use only if benefit outweighs risk.\n\tExplanation: (TODO)", 
          $de1.getPersonId());
        System.out.println(s);
  s = String.format("DATA\t%s\t%s\tclonidine and a beta-blocker interaction\tclonidine, timolol, systemic yes", currentDateStr, $person.getPersonId());
       System.out.println(s);
end
*/

/*
Acebutolol
Atenolol
Bisoprolol
Esmolol
Metoprolol
Nebivolol
Betaxolol
Carvedilol
Labetalol
result in no special precautions
*/

