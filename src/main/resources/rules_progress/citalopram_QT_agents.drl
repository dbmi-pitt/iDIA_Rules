// Citalopram - QT Prolonging Agents DDI Alerting Rule

package com.sample

//list any import classes here.
import function com.sample.DRLLogger.*;

import java.util.*;
import java.lang.String;
import java.sql.Timestamp;

import com.sample.model.ConceptSetItem;
import com.sample.model.RHSState;

import edu.pitt.dbmi.ohdsiv5.db.ConditionEra;
import edu.pitt.dbmi.ohdsiv5.db.DrugEra;
import edu.pitt.dbmi.ohdsiv5.db.DrugExposure;
import edu.pitt.dbmi.ohdsiv5.db.DrugStrength;
import edu.pitt.dbmi.ohdsiv5.db.ExtendedDrugExposure;
import edu.pitt.dbmi.ohdsiv5.db.Measurement;
import edu.pitt.dbmi.ohdsiv5.db.Person;


/////////// 
// declare any global variables here
/////////// 
global java.lang.String currentDateStr;
global java.sql.Timestamp currentDate;
global java.sql.Timestamp within48hours;
global java.sql.Timestamp within28days;
global java.sql.Timestamp plus1day;


/////////// 
// declare any types here
/////////// 


/////////// 
// Rules
/////////// 

/*
concept sets:
11466 QT-Agents Ingredients
11441 QT-Agents

12114 Citaloprams Ingredients
12115 Citaloprams
*/

// basic check to identify all patients on any dosage or form of citalopram. (QT-agent not necessary to cause an alert)
rule "CITALOPRAM - QT-AGENT -- NO FILTER"
    when
      // First, we look for patients exposed to citalopram on the ingredient level
      $clinDrugConcept1 : ConceptSetItem(csName == "Citaloprams Ingredients")
      $de1 : DrugEra(drugConceptId == $clinDrugConcept1.getConceptId()) 
      // Second, we similarly look for exposure to a QT-agent on the ingredient level
      $clinDrugConcept2 : ConceptSetItem(csName == "QT-Agents Ingredients")
      $de2 : DrugEra(PersonId == $de1.getPersonId() && drugConceptId == $clinDrugConcept2.getConceptId())
      // Next, we identify the specific drug exposures for both citalopram and the QT-agent
      $clinDrugConcept3 : ConceptSetItem(csName == "Citaloprams") 
      $dexp1 : ExtendedDrugExposure(personId != null && personId == $de1.getPersonId()
          && drugExposureStartDate != null && drugExposureStartDate >= currentDate && drugExposureStartDate < plus1day
          && drugConceptId == $clinDrugConcept3.getConceptId() && ingredientConceptId == $clinDrugConcept1.getConceptId())
      $clinDrugConcept4 : ConceptSetItem(csName == "QT-Agents")
      $dexp2 : ExtendedDrugExposure(personId != null && personId == $de1.getPersonId()
          && (drugExposureStartDate != null && drugExposureStartDate <= $dexp1.getDrugExposureEndDate()
          || drugExposureEndDate != null && drugExposureEndDate >= $dexp1.getDrugExposureStartDate())
          && drugConceptId != null && drugConceptId == $clinDrugConcept4.getConceptId() 
          && ingredientConceptId != null && ingredientConceptId == $clinDrugConcept2.getConceptId())
      // This creates the patient as an object that can be used in the RHS state in the right hand side of the rule
      $person : Person(personId == $de1.getPersonId())
    then
      RHSState rhsCur = new RHSState("basic concomitant exposure of citalopram and a QT-agent", "yes", $person);
      insertLogical(rhsCur);
      // The output string reports simply that a potential interaction was identified through concurrent drug exposures and lists the drug IDs
      String s = String.format(
          "Matched drug exposures for the patient at the clinical drug level: Patient ID: %s; Citalopram: %s; QT-Agent: %s.", 
          $de1.getPersonId(), 
          $clinDrugConcept3.getConceptId(), 
          $clinDrugConcept4.getConceptId());
      System.out.println(s);
      s = String.format("DATA\t%s\t%s\tcitalopram and a QT-agent interaction\tbasic concomitant exposure", currentDateStr, $person.getPersonId());
     System.out.println(s);
end

// basic check to identify all patients on only citalopram at a dose of 60 or more mg/day
rule "CITALOPRAM -- Citalopram High Dosage"
    when
      $clinDrugConcept1 : ConceptSetItem(csName == "Citaloprams Ingredients")
      $de1 : DrugEra(drugConceptId == $clinDrugConcept1.getConceptId()) 

      $clinDrugConcept2 : ConceptSetItem(csName == "Citaloprams") 
      $dexp1 : ExtendedDrugExposure(personId != null && personId == $de1.getPersonId()
          && drugExposureStartDate != null && drugExposureStartDate >= currentDate && drugExposureStartDate < plus1day
          && drugConceptId == $clinDrugConcept3.getConceptId() && ingredientConceptId == $clinDrugConcept1.getConceptId()
          && dailyDosage >= 60 && doseUnitSourcevalue == 'mg') // TODO can we rely on this field to be filled in?
      $person : Person(personId == $de1.getPersonId());
    then
      RHSState rhsNN = new RHSState("citalopram, high dosage", "yes", $person);
      insertLogical(rhsNN);
      String s = String.format(
          "Matched drug exposures for the patient at the clinical drug level: Patient ID: %s; Citalopram: %s; Citalopram Dosage: %s", 
          $de1.getPersonId(), 
          $clinDrugConcept2.getConceptId(), 
          $clinDrugConcept2.getDailyDosage());
      System.out.println(s);
      s = String.format("DATA\t%s\t%s\tcitalopram high dosage", currentDateStr, $person.getPersonId());
     System.out.println(s);
end
