//Last Updated 2017-01-13
package com.sample

//list any import classes here.
import function com.sample.DRLLogger.*;

import java.util.List;
import java.util.ArrayList;
import java.util.Map;
import java.util.Calendar;
import java.lang.String;

import com.sample.model.ConceptSetItem;
import com.sample.model.RHSState;

import edu.pitt.dbmi.ohdsiv5.db.ConditionEra;
import edu.pitt.dbmi.ohdsiv5.db.DrugEra;
import edu.pitt.dbmi.ohdsiv5.db.DrugExposure;
import edu.pitt.dbmi.ohdsiv5.db.DrugStrength;
import edu.pitt.dbmi.ohdsiv5.db.ExtendedDrugExposure;
import edu.pitt.dbmi.ohdsiv5.db.Measurement;
import edu.pitt.dbmi.ohdsiv5.db.Person;


/////////// 
// declare any global variables here
/////////// 
global org.hibernate.Session hibernateSession;
global java.util.Calendar currentDate;
global java.util.Calendar within48date;


/////////// 
// declare any types here
/////////// 


/////////// 
// Rules
/////////// 

rule "EPINEPHRINE - BETA-BLOCKERS -- NO FILTER"
    when
      // Epinephrine ingredient drug era
      $clinDrugConcept1 : ConceptSetItem(csName == "Epinephrines Ingredients")
      $de1 : DrugEra(drugConceptId == $clinDrugConcept1.getConceptId()) 
      
      // Beta-blocker ingredient drug era
      $clinDrugConcept2 : ConceptSetItem(csName == "Beta-Blockers Ingredients")
      $de2 : DrugEra(PersonId == $de1.getPersonId() && drugConceptId == $clinDrugConcept2.getConceptId())

      // Specific epinephrine exposure
      $clinDrugConcept3 : ConceptSetItem(csName == "Epinephrines") // concept set excludes opthalmic and inhaled epinephrine
      $dexp1 : ExtendedDrugExposure(personId == $de1.getPersonId() && drugExposureStartDate == $de1.getDrugEraStartDate() 
      && drugConceptId == $clinDrugConcept3.getConceptId() && ingredientConceptId == $clinDrugConcept1.getConceptId())
   
      // Specific beta-blocker exposure
      $clinDrugConcept4 : ConceptSetItem(csName == "Beta-Blockers")
      $dexp2 : ExtendedDrugExposure(personId == $de1.getPersonId() && drugExposureStartDate == $de2.getDrugEraStartDate() 
      && drugConceptId == $clinDrugConcept4.getConceptId() && ingredientConceptId == $clinDrugConcept2.getConceptId())

      $person : Person(personId == $de1.getPersonId())
    then	
      // Create a state object for the current node in the decision tree to be used by other LHS
      RHSState rhsCur = new RHSState("basic concomitant exposure of epinephrine and a beta-blocker", "yes", $person);
      insertLogical(rhsCur);
    
      String s = String.format(
          "Matched drug exposures for the patient at the clinical drug level: Patient ID: %s; Epinephrine: %s; Beta-Blocker: %s.", 
          $de1.getPersonId(), 
          $clinDrugConcept3.getConceptId(), 
          $clinDrugConcept4.getConceptId());
      System.out.println(s);
end

rule "EPINEPHRINE - BETA-BLOCKERS -- Non-Selective Beta-Blockers"
    when
	// Fist check if patient was exposed to epinephrine and a beta-blockers
        $rhs : RHSState(stateName == "basic concomitant exposure of epinephrine and a beta-blocker" && state == "yes", $person : person)
        // Non-selective BB drug era
        $clinDrugConcept1 : ConceptSetItem(csName == "Non-Selective Beta-Blockers Ingredients") // includes Timolol
        $de1 : DrugEra(personId == $person.getPersonId() && drugConceptId == $clinDrugConcept1.getConceptId())        
        // Specific non-selective BB exposure
        $clinDrugConcept2 : ConceptSetItem(csName == "Non-Selective Beta-Blockers") // includes Timolol oral, eliminating the need for a separate rule
        $dexp1 : ExtendedDrugExposure(personId == $person.getPersonId() && drugExposureStartDate == $de1.getDrugEraStartDate() && drugConceptId == $clinDrugConcept2.getConceptId() && ingredientConceptId == $clinDrugConcept1.getConceptId())                
    then	
        RHSState rhsNN = new RHSState("non-selective beta-blockers", "yes", $rhs.getPerson());
        insertLogical(rhsNN);
        
        String s = String.format(
          "Epinephrine - Beta-blocker interaction for patient: %s.\n\tClinical implication: High risk of acute hypertensive reaction.\n\tMitigating factor: Presence of a non-selective beta-blocker (%s).\n\tRecommendation: Avoid if possible, treat with cardioselective beta-blocker instead.\n\tExplanation: When a systemic dose of epinephrine is given to a person on one of these nonselective beta-blockers, an acute hypertensive reaction is almost certain. Systolic BPs of 250 mm/Hg are not uncommon. \n\tMost people can probably withstand a short episode of such a hypertensive reaction without permanent sequelae, but strokes have occurred in susceptible patients.", 
          $de1.getPersonId(), 
          $dexp1.getDrugConceptId());
        System.out.println(s);
end

rule "EPINEPHRINE - BETA-BLOCKERS -- Timolol (CYP2D6 Inhibitors)"
    when
	// Fist check if patient was exposed to epinephrine and a beta-blockers
        $rhs : RHSState(stateName == "basic concomitant exposure of epinephrine and a beta-blocker" && state == "yes", $person : person)
        // Timolol drug era
        $clinDrugConcept1 : ConceptSetItem(csName == "Timolols Ingredients")
        $de1 : DrugEra(personId == $person.getPersonId() && drugConceptId == $clinDrugConcept1.getConceptId())        
        // Timolol drug exposure
        $clinDrugConcept2 : ConceptSetItem(csName == "Timolols Eye Drops")
        $dexp1 : ExtendedDrugExposure(personId == $person.getPersonId() && drugExposureStartDate == $de1.getDrugEraStartDate() && drugConceptId == $clinDrugConcept2.getConceptId() && ingredientConceptId == $clinDrugConcept1.getConceptId())      
	// CYP2D6 inhibitor drug era
        $clinDrugConcept3 : ConceptSetItem(csName == "CYP2D6 Inhibitors Ingredients")
        $de2 : DrugEra(personId == $person.getPersonId() && drugConceptId == $clinDrugConcept3.getConceptId())        
        // CYP2D6 inhibitor drug exposure
        $clinDrugConcept4 : ConceptSetItem(csName == "CYP2D6 Inhibitors")
        $dexp2 : ExtendedDrugExposure(personId == $person.getPersonId() && drugExposureStartDate == $de2.getDrugEraStartDate() && drugConceptId == $clinDrugConcept4.getConceptId() && ingredientConceptId == $clinDrugConcept3.getConceptId())   
    then	
        RHSState rhsNN = new RHSState("timolol eye drops and cyp2d6 inhibitor", "yes", $rhs.getPerson());
        insertLogical(rhsNN);
        
        String s = String.format(
          "Epinephrine - Beta-blocker interaction for patient: %s.\n\tClinical implication: Risk of acute hypertensive reaction.\n\tMitigating factor: Timolol eye drops given in presence of potent CYP2D6 inhibitor and epinephrine.\n\tRecommendation: Use only if benefit outweighs risk.\n\tExplanation: Timolol is a nonselective beta-blocker, and timolol eye drops have been shown to produce systemic beta-blockade. Timolol is metabolized by CYP2D6, and patients who are taking moderate to potent \n\tCYP2D6 inhibitors may develop higher timolol plasma concentrations following timolol ophthalmic aqueous drops, which could lead to an acute hypertensive reaction.", 
          $de1.getPersonId());
        System.out.println(s);
end

rule "EPINEPHRINE - BETA-BLOCKERS -- Timolol (No CYP2D6 Inhibitors)"
    when
	// Fist check if patient was exposed to epinephrine and a beta-blockers
        $rhs : RHSState(stateName == "basic concomitant exposure of epinephrine and a beta-blocker" && state == "yes", $person : person)	
	// Insure that the metropolol dosage is not above the threshold
	not ($rhs2 : RHSState(stateName == "timolol eye drops and cyp2d6 inhibitor" && state == "yes" && person.getPersonId() == $person.getPersonId()))
        // Timolol drug era
        $clinDrugConcept1 : ConceptSetItem(csName == "Timolols Ingredients")
        $de1 : DrugEra(personId == $person.getPersonId() && drugConceptId == $clinDrugConcept1.getConceptId())        
        // Timolol drug exposure
        $clinDrugConcept2 : ConceptSetItem(csName == "Timolols Eye Drops")
        $dexp1 : ExtendedDrugExposure(personId == $person.getPersonId() && drugExposureStartDate == $de1.getDrugEraStartDate() && drugConceptId == $clinDrugConcept2.getConceptId() && ingredientConceptId == $clinDrugConcept1.getConceptId()) 
    then	
        RHSState rhsNN = new RHSState("timolol eye drops", "yes", $rhs.getPerson());
        insertLogical(rhsNN);
        
        String s = String.format(
          "Epinephrine - Beta-blocker interaction for patient: %s.\n\tClinical implication: Risk of acute hypertensive reaction.\n\tMitigating factor: Timolol eye drops given with epinephrine.\n\tRecommendation: Assess risk and take action if necessary.\n\tExplanation: Many cases of systemic beta-blockade have been reported with aqueous timolol eye drops, so a hypertensive reaction is possible.", 
          $de1.getPersonId(), 
          $dexp1.getDailyDosage());
        System.out.println(s);
end

rule "EPINEPHRINE - BETA-BLOCKERS -- Atenolol (Clearance)"
    when
	// Fist check if patient was exposed to epinephrine and a beta-blockers
        $rhs : RHSState(stateName == "basic concomitant exposure of epinephrine and a beta-blocker" && state == "yes", $person : person)
        // Atenolol drug era
        $clinDrugConcept1 : ConceptSetItem(csName == "Atenolols Ingredients")
        $de1 : DrugEra(personId == $person.getPersonId() && drugConceptId == $clinDrugConcept1.getConceptId())        
        // Atenolol drug exposure
        $clinDrugConcept2 : ConceptSetItem(csName == "Atenolols")
        $dexp1 : ExtendedDrugExposure(personId == $person.getPersonId() && drugExposureStartDate == $de1.getDrugEraStartDate() && drugConceptId == $clinDrugConcept2.getConceptId() && ingredientConceptId == $clinDrugConcept1.getConceptId())
	// Creatinine clearance level check, alert if below 40
	$measurement : Measurement(personId == $person.getPersonId() && measurementConceptId == 3016723 && valueAsNumber < 40 && unitConceptId == 8795)
    then	
        RHSState rhsNN = new RHSState("atenolol and low clearance", "yes", $rhs.getPerson());
        insertLogical(rhsNN);
        
        String s = String.format(
          "Epinephrine - Beta-blocker interaction for patient: %s.\n\tClinical implication: Risk of acute hypertensive reaction.\n\tMitigating factor: Atenolol and a low creatinine clearance level (%s mL/min, compared to the threshold value of 40 mL/min).\n\tRecommendation: Assess risk and take action if necessary.\n\tExplanation: Atenolol undergoes renal elimination and may produce nonselective beta-blockade with significant renal impairment, which can cause an acute hypertensive reaction.", 
          $de1.getPersonId(), 
          $measurement.getValueAsNumber());
        System.out.println(s);
end

rule "EPINEPHRINE - BETA-BLOCKERS -- Atenolol (Dosage)"
    when
	// Fist check if patient was exposed to epinephrine and a beta-blockers
        $rhs : RHSState(stateName == "basic concomitant exposure of epinephrine and a beta-blocker" && state == "yes", $person : person)	
	// Next, make sure the patient isn't renally impaired
	not ($rhs2 : RHSState(stateName == "atenolol and low clearance" && state == "yes" && person.getPersonId() == $person.getPersonId()))
        // Atenolol drug era
        $clinDrugConcept1 : ConceptSetItem(csName == "Atenolols Ingredients")
        $de1 : DrugEra(personId == $person.getPersonId() && drugConceptId == $clinDrugConcept1.getConceptId())        
        // Atenolol drug exposure and dosage check, alert if above 100 mg/day
        $clinDrugConcept2 : ConceptSetItem(csName == "Atenolols")
        $dexp1 : ExtendedDrugExposure(personId == $person.getPersonId() && drugExposureStartDate == $de1.getDrugEraStartDate() && drugConceptId == $clinDrugConcept2.getConceptId() 
	  && ingredientConceptId == $clinDrugConcept1.getConceptId() && dailyDosage > 100 )                
    then	
        RHSState rhsNN = new RHSState("high atenolol dosage", "yes", $rhs.getPerson());
        insertLogical(rhsNN);
        
        String s = String.format(
          "Epinephrine - Beta-blocker interaction for patient: %s.\n\tClinical implication: Risk of acute hypertensive reaction.\n\tMitigating factor: High dosage of atenolol (Daily Dosage: %s mg/day, compared to the threshold value of 100.0 mg/day).\n\tRecommendation: Assess risk and take action if necessary.\n\tExplanation: At doses over 100 mg/day, atenolol can result in nonselective beta-blockade, which can cause an acute hypertensive reaction.", 
          $de1.getPersonId(), 
          $dexp1.getDailyDosage());
        System.out.println(s);
end

rule "EPINEPHRINE - BETA-BLOCKERS -- Bisoprolol (Clearance)"
    when
	// Fist check if patient was exposed to epinephrine and a beta-blockers
        $rhs : RHSState(stateName == "basic concomitant exposure of epinephrine and a beta-blocker" && state == "yes", $person : person)
        // Bisoprolol drug era
        $clinDrugConcept1 : ConceptSetItem(csName == "Bisoprolols Ingredients")
        $de1 : DrugEra(personId == $person.getPersonId() && drugConceptId == $clinDrugConcept1.getConceptId())        
        // Bisoprolol drug exposure
        $clinDrugConcept2 : ConceptSetItem(csName == "Bisoprolols")
        $dexp1 : ExtendedDrugExposure(personId == $person.getPersonId() && drugExposureStartDate == $de1.getDrugEraStartDate() && drugConceptId == $clinDrugConcept2.getConceptId() && ingredientConceptId == $clinDrugConcept1.getConceptId())
	// Creatinine clearance level check, alert if below 40
	$measurement : Measurement(personId == $person.getPersonId() && measurementConceptId == 3016723 && valueAsNumber < 40 && unitConceptId == 8795)
    then	
        RHSState rhsNN = new RHSState("bisoprolol and low clearance", "yes", $rhs.getPerson());
        insertLogical(rhsNN);
        
        String s = String.format(
          "Epinephrine - Beta-blocker interaction for patient: %s.\n\tClinical implication: Risk of acute hypertensive reaction.\n\tMitigating factor: Bisoprolol and a low creatinine clearance level (%s mL/min, compared to the threshold value of 40 mL/min).\n\tRecommendation: Assess risk and take action if necessary.\n\tExplanation: Bisoprolol undergoes renal elimination and may produce nonselective beta-blockade with significant renal impairment, which can cause an acute hypertensive reaction.", 
          $de1.getPersonId(), 
          $measurement.getValueAsNumber());
        System.out.println(s);
end

rule "EPINEPHRINE - BETA-BLOCKERS -- Bisoprolol (Dosage)"
    when
	// Fist check if patient was exposed to epinephrine and a beta-blockers
        $rhs : RHSState(stateName == "basic concomitant exposure of epinephrine and a beta-blocker" && state == "yes", $person : person)	
	// Next, make sure the patient isn't renally impaired
	not ($rhs2 : RHSState(stateName == "bisoprolol and low clearance" && state == "yes" && person.getPersonId() == $person.getPersonId()))
        // Bisoprolol drug era
        $clinDrugConcept1 : ConceptSetItem(csName == "Bisoprolols Ingredients")
        $de1 : DrugEra(personId == $person.getPersonId() && drugConceptId == $clinDrugConcept1.getConceptId())        
        // Bisoprolol drug exposure and dosage check, alert if above 20 mg/day
        $clinDrugConcept2 : ConceptSetItem(csName == "Bisoprolols")
        $dexp1 : ExtendedDrugExposure(personId == $person.getPersonId() && drugExposureStartDate == $de1.getDrugEraStartDate() && drugConceptId == $clinDrugConcept2.getConceptId() 
	  && ingredientConceptId == $clinDrugConcept1.getConceptId() && dailyDosage > 20 )                
    then	
        RHSState rhsNN = new RHSState("high bisoprolol dosage", "yes", $rhs.getPerson());
        insertLogical(rhsNN);
        
        String s = String.format(
          "Epinephrine - Beta-blocker interaction for patient: %s.\n\tClinical implication: Risk of acute hypertensive reaction.\n\tMitigating factor: High dosage of bisoprolol (Daily Dosage: %s mg/day, compared to the threshold value of 20.0 mg/day).\n\tRecommendation: Assess risk and take action if necessary.\n\tExplanation: At doses of 20 mg/day or higher, bisoprolol can result in nonselective beta-blockade, which can cause an acute hypertensive reaction.", 
          $de1.getPersonId(), 
          $dexp1.getDailyDosage());
        System.out.println(s);
end

rule "EPINEPHRINE - BETA-BLOCKERS -- Esmolol" // THIS RULE IS INCOMPLETE, NOT FUNCTIONING CORRECTLY
    when
	// Fist check if patient was exposed to epinephrine and a beta-blockers
        $rhs : RHSState(stateName == "basic concomitant exposure of epinephrine and a beta-blocker" && state == "yes", $person : person)	
        // Esmolol drug era
        $clinDrugConcept1 : ConceptSetItem(csName == "Esmolols Ingredients")
        $de1 : DrugEra(personId == $person.getPersonId() && drugConceptId == $clinDrugConcept1.getConceptId())        
        // Esmolol drug exposure and dosage check, alert if above 300 mcg/kg/ml
        $clinDrugConcept2 : ConceptSetItem(csName == "Esmolols")
        $dexp1 : ExtendedDrugExposure(personId == $person.getPersonId() && drugExposureStartDate == $de1.getDrugEraStartDate() && drugConceptId == $clinDrugConcept2.getConceptId() 
	  && ingredientConceptId == $clinDrugConcept1.getConceptId() && dailyDosage > 300 )                
    then	
        RHSState rhsNN = new RHSState("high esmolol dosage", "yes", $rhs.getPerson());
        insertLogical(rhsNN);
        
        String s = String.format(
          "Epinephrine - Beta-blocker interaction for patient: %s.\n\tClinical implication: Risk of acute hypertensive reaction.\n\tMitigating factor: High dosage of esmolol (Daily Dosage: %s mcg/kg/min, compared to the threshold value of 300.0 mcg/kg/min).\n\tRecommendation: Assess risk and take action if necessary.\n\tExplanation: At doses over 300 mcg/kg/min, esmolol can result in nonselective beta-blockade, which can cause an acute hypertensive reaction.", 
          $de1.getPersonId(), 
          $dexp1.getDailyDosage());
        System.out.println(s);
end

rule "EPINEPHRINE - BETA-BLOCKERS -- Metoprolol (Dosage)"
    when
	// Fist check if patient was exposed to epinephrine and a beta-blockers
        $rhs : RHSState(stateName == "basic concomitant exposure of epinephrine and a beta-blocker" && state == "yes", $person : person)	
        // Metoprolol drug era
        $clinDrugConcept1 : ConceptSetItem(csName == "Metoprolols Ingredients")
        $de1 : DrugEra(personId == $person.getPersonId() && drugConceptId == $clinDrugConcept1.getConceptId())        
        // Metoprolol drug exposure and dosage check, alert if above 100 mg/day
        $clinDrugConcept2 : ConceptSetItem(csName == "Metoprolols")
        $dexp1 : ExtendedDrugExposure(personId == $person.getPersonId() && drugExposureStartDate == $de1.getDrugEraStartDate() && drugConceptId == $clinDrugConcept2.getConceptId() 
	  && ingredientConceptId == $clinDrugConcept1.getConceptId() && dailyDosage > 100 )                
    then	
        RHSState rhsNN = new RHSState("high metoprolol dosage", "yes", $rhs.getPerson());
        insertLogical(rhsNN);
        
        String s = String.format(
          "Epinephrine - Beta-blocker interaction for patient: %s.\n\tClinical implication: Risk of acute hypertensive reaction.\n\tMitigating factor: High dosage of metoprolol (Daily Dosage: %s mg/day, compared to the threshold value of 100.0 mg/day).\n\tRecommendation: Assess risk and take action if necessary.\n\tExplanation: At doses over 100 mg/day, metoprolol can result in nonselective beta-blockade, which can cause an acute hypertensive reaction.", 
          $de1.getPersonId(), 
          $dexp1.getDailyDosage());
        System.out.println(s);
end

rule "EPINEPHRINE - BETA-BLOCKERS -- Metoprolol (CYP2D6 Inhibitors)"
    when
	// Fist check if patient was exposed to epinephrine and a beta-blockers
        $rhs : RHSState(stateName == "basic concomitant exposure of epinephrine and a beta-blocker" && state == "yes", $person : person)	
	// Insure that the metropolol dosage is not above the threshold
	not ($rhs2 : RHSState(stateName == "high metoprolol dosage" && state == "yes" && person.getPersonId() == $person.getPersonId()))
        // Metoprolol drug era
        $clinDrugConcept1 : ConceptSetItem(csName == "Metoprolols Ingredients")
        $de1 : DrugEra(personId == $person.getPersonId() && drugConceptId == $clinDrugConcept1.getConceptId())        
        // Metoprolol drug exposure
        $clinDrugConcept2 : ConceptSetItem(csName == "Metoprolols")
        $dexp1 : ExtendedDrugExposure(personId == $person.getPersonId() && drugExposureStartDate == $de1.getDrugEraStartDate() && drugConceptId == $clinDrugConcept2.getConceptId() && ingredientConceptId == $clinDrugConcept1.getConceptId())      
	// CYP2D6 inhibitor drug era
        $clinDrugConcept3 : ConceptSetItem(csName == "CYP2D6 Inhibitors Ingredients")
        $de2 : DrugEra(personId == $person.getPersonId() && drugConceptId == $clinDrugConcept3.getConceptId())        
        // CYP2D6 inhibitor drug exposure
        $clinDrugConcept4 : ConceptSetItem(csName == "CYP2D6 Inhibitors")
        $dexp2 : ExtendedDrugExposure(personId == $person.getPersonId() && drugExposureStartDate == $de2.getDrugEraStartDate() && drugConceptId == $clinDrugConcept4.getConceptId() && ingredientConceptId == $clinDrugConcept3.getConceptId())   
    then	
        RHSState rhsNN = new RHSState("metoprolol and cyp2d6 inhibitor", "yes", $rhs.getPerson());
        insertLogical(rhsNN);
        
        String s = String.format(
          "Epinephrine - Beta-blocker interaction for patient: %s.\n\tClinical implication: Risk of acute hypertensive reaction.\n\tMitigating factor: Metoprolol given in presence of potent CYP2D6 inhibitor.\n\tRecommendation: Assess risk and take action if necessary.\n\tExplanation: Metoprolol is metabolized by CYP2D6, and patients who are receiving moderate to potent CYP2D6 inhibitors may develop higher \n\tmetoprolol plasma concentrations and nonselective beta-blockade, which could lead to an acute hypertensive reaction.", 
          $de1.getPersonId(), 
          $dexp1.getDailyDosage());
        System.out.println(s);
end

rule "EPINEPHRINE - BETA-BLOCKERS -- Nebivolol (Dosage)"
    when
	// Fist check if patient was exposed to epinephrine and a beta-blockers
        $rhs : RHSState(stateName == "basic concomitant exposure of epinephrine and a beta-blocker" && state == "yes", $person : person)	
        // Nebivolol drug era
        $clinDrugConcept1 : ConceptSetItem(csName == "Nebivolols Ingredients")
        $de1 : DrugEra(personId == $person.getPersonId() && drugConceptId == $clinDrugConcept1.getConceptId())        
        // Nebivolol drug exposure and dosage check, alert if above 10 mg/day
        $clinDrugConcept2 : ConceptSetItem(csName == "Nebivolols")
        $dexp1 : ExtendedDrugExposure(personId == $person.getPersonId() && drugExposureStartDate == $de1.getDrugEraStartDate() && drugConceptId == $clinDrugConcept2.getConceptId() 
	  && ingredientConceptId == $clinDrugConcept1.getConceptId() && dailyDosage > 10 )                
    then	
        RHSState rhsNN = new RHSState("high nebivolol dosage", "yes", $rhs.getPerson());
        insertLogical(rhsNN);
        
        String s = String.format(
          "Epinephrine - Beta-blocker interaction for patient: %s.\n\tClinical implication: Risk of acute hypertensive reaction.\n\tMitigating factor: High dosage of nebivolol (Daily Dosage: %s mg/day, compared to the threshold value of 10.0 mg/day).\n\tRecommendation: Assess risk and take action if necessary.\n\tExplanation: At doses over 10 mg/day, nebivolol can result in nonselective beta-blockade, which can cause an acute hypertensive reaction.", 
          $de1.getPersonId(), 
          $dexp1.getDailyDosage());
        System.out.println(s);
end

rule "EPINEPHRINE - BETA-BLOCKERS -- Nebivolol (CYP2D6 Inhibitors)"
    when
	// Fist check if patient was exposed to epinephrine and a beta-blockers
        $rhs : RHSState(stateName == "basic concomitant exposure of epinephrine and a beta-blocker" && state == "yes", $person : person)	
	// Insure that the metropolol dosage is not above the threshold
	not ($rhs2 : RHSState(stateName == "high nebivolol dosage" && state == "yes" && person.getPersonId() == $person.getPersonId()))
        // Nebivolol drug era
        $clinDrugConcept1 : ConceptSetItem(csName == "Nebivolols Ingredients")
        $de1 : DrugEra(personId == $person.getPersonId() && drugConceptId == $clinDrugConcept1.getConceptId())        
        // Nebivolol drug exposure
        $clinDrugConcept2 : ConceptSetItem(csName == "Nebivolols")
        $dexp1 : ExtendedDrugExposure(personId == $person.getPersonId() && drugExposureStartDate == $de1.getDrugEraStartDate() && drugConceptId == $clinDrugConcept2.getConceptId() && ingredientConceptId == $clinDrugConcept1.getConceptId())      
	// CYP2D6 inhibitor drug era
        $clinDrugConcept3 : ConceptSetItem(csName == "CYP2D6 Inhibitors Ingredients")
        $de2 : DrugEra(personId == $person.getPersonId() && drugConceptId == $clinDrugConcept3.getConceptId())        
        // CYP2D6 inhibitor drug exposure
        $clinDrugConcept4 : ConceptSetItem(csName == "CYP2D6 Inhibitors")
        $dexp2 : ExtendedDrugExposure(personId == $person.getPersonId() && drugExposureStartDate == $de2.getDrugEraStartDate() && drugConceptId == $clinDrugConcept4.getConceptId() && ingredientConceptId == $clinDrugConcept3.getConceptId())   
    then	
        RHSState rhsNN = new RHSState("nebivolol and cyp2d6 inhibitor", "yes", $rhs.getPerson());
        insertLogical(rhsNN);
        
        String s = String.format(
          "Epinephrine - Beta-blocker interaction for patient: %s.\n\tClinical implication: Risk of acute hypertensive reaction.\n\tMitigating factor: Nebivolol given in presence of a potent CYP2D6 inhibitor.\n\tRecommendation: Assess risk and take action if necessary.\n\tExplanation: Nebivolol is metabolized by CYP2D6, and patients who are receiving moderate to potent CYP2D6 inhibitors may develop higher \n\tnebivolol plasma concentrations and nonselective beta-blockade, which could lead to an acute hypertensive reaction.", 
          $de1.getPersonId(), 
          $dexp1.getDailyDosage());
        System.out.println(s);
end

rule "EPINEPHRINE - BETA-BLOCKERS -- Betaxolol"
    when
	// Fist check if patient was exposed to epinephrine and a beta-blockers
        $rhs : RHSState(stateName == "basic concomitant exposure of epinephrine and a beta-blocker" && state == "yes", $person : person)
        // Betaxolol drug era
        $clinDrugConcept1 : ConceptSetItem(csName == "Betaxolols Ingredients")
        $de1 : DrugEra(personId == $person.getPersonId() && drugConceptId == $clinDrugConcept1.getConceptId())        
        // Betaxolol drug exposure and dosage check (no alert if below threshold
        $clinDrugConcept2 : ConceptSetItem(csName == "Betaxolols")
        $dexp1 : ExtendedDrugExposure(personId == $person.getPersonId() && drugExposureStartDate == $de1.getDrugEraStartDate() && drugConceptId == $clinDrugConcept2.getConceptId() 
	  && ingredientConceptId == $clinDrugConcept1.getConceptId() && dailyDosage > 20 )                
    then	
        RHSState rhsNN = new RHSState("high betaxolol dose", "yes", $rhs.getPerson());
        insertLogical(rhsNN);
        
        String s = String.format(
          "Epinephrine - Beta-blocker interaction for patient: %s.\n\tClinical implication: Risk of acute hypertensive reaction.\n\tMitigating factor: High dosage of betaxolol (Daily Dosage: %s mg/day, compared to the threshold value of 20.0 mg/day).\n\tRecommendation: Assess risk and take action if necessary.\n\tExplanation: Large doses of betaxolol may result in nonselective beta-blockade, which can cause an acute hypertensive reaction.", 
          $de1.getPersonId(), 
          $dexp1.getDailyDosage());
        System.out.println(s);
end