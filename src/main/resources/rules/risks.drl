//created on: Feb 12, 2016
package com.sample

//list any import classes here.
import function com.sample.DRLLogger.*;

import java.util.List;
import java.util.ArrayList;
import java.util.Map;
import java.lang.String;

import com.sample.model.ConceptSetItem;
import com.sample.model.RHSState;

import edu.pitt.dbmi.ohdsiv5.db.ConditionEra;
import edu.pitt.dbmi.ohdsiv5.db.DrugExposure;
import edu.pitt.dbmi.ohdsiv5.db.DrugEra;
import edu.pitt.dbmi.ohdsiv5.db.Person;

/////////// 
// declare any global variables here
/////////// 
global org.hibernate.Session hibernateSession;


/////////// 
// declare any types here
/////////// 


/////////// 
// Rules
/////////// 
rule "DEBUGGING ONLY -- warfarin - ibuprofen INGREDIENT exposure"
    when
      $de1 : DrugEra(drugConceptId == 1310149)
      $de2 : DrugEra(personId == $de1.getPersonId() && drugConceptId == 1177480)
      $person : Person(personId == $de1.getPersonId() && yearOfBirth <= 1951)
    then	
      System.out.println("matched drug era ids for patient (ingredient): " + $de1.getPersonId() + "; year of birth: " + $person.getYearOfBirth());
end


rule "WARFARIN - NSAID -- NO FILTER" // Concept sets exclude topical diclofenac so no need to include the related logic
    when
      // Warfarin ingredient drug era
      $de1 : DrugEra(drugConceptId == 1310149) 
      
      // NSAID ingredient drug era -- NOTE: the data is loaded for a
      //specific date. So, that the presence of this drug era
      //indicates concomitant exposure
      $de2 : DrugEra(personId == $de1.getPersonId() && 
                     drugConceptId == 1177480)

      // Specific warfarin exposure - DrugExposures often only have
      //the start date so the DrugEra is used to infer the exposure
      //period
      $clinDrugConcept1 : ConceptSetItem(csName == "Warfarins")
      $dexp1 : DrugExposure(personId == $de1.getPersonId() && 
                            drugExposureStartDate == $de1.getDrugEraStartDate() && 
                            drugConceptId == $clinDrugConcept1.getConceptId())
   
      // Specific NSAID exposure
      $clinDrugConcept2 : ConceptSetItem(csName == "NSAIDs")
      $dexp2 : DrugExposure(personId == $de1.getPersonId() && 
                            drugExposureStartDate == $de2.getDrugEraStartDate() && 
                            drugConceptId == $clinDrugConcept2.getConceptId())

      $person : Person(personId == $de1.getPersonId())
    then	
      // Create a state object for the current node in the decision tree to be used by other LHS
      RHSState rhsCur = new RHSState("basic concomitant exposure of warfarin and NSAID", "yes", $person);
      insertLogical(rhsCur);
    
      String s = String.format(
          "matched drug EXPOSURE ids for patient (clinical drug): %s; anticoagulant: %s; ibuprofen: %s", 
          $de1.getPersonId(), 
          $clinDrugConcept1.getConceptId(), 
          $clinDrugConcept2.getConceptId());
      System.out.println(s);
end

rule "WARFARIN - NSAID -- PPI or misoprostol"
    when
        $rhs : RHSState(stateName == "basic concomitant exposure of warfarin and NSAID" && state == "yes")
        
        // PPI or Misoprostol drug era
        $clinDrugConcept1 : ConceptSetItem(csName == "Proton Pump Inhibitor Ingredients")
        $clinDrugConcept2 : ConceptSetItem(csName == "Misoprostols Ingredients")
        $de1 : DrugEra(personId == $rhs.getPerson().getPersonId() && 
                              (drugConceptId == $clinDrugConcept1.getConceptId() || drugConceptId == $clinDrugConcept2.getConceptId())
                              )
        
        // Specific PPI or Misoprostol exposure 
        $clinDrugConcept3 : ConceptSetItem(csName == "Proton Pump Inhibitors")
        $clinDrugConcept4 : ConceptSetItem(csName == "Misoprostols")
        $dexp1 : DrugExposure(personId == $rhs.getPerson().getPersonId() && 
                              drugExposureStartDate == $de1.getDrugEraStartDate() &&
                              (drugConceptId == $clinDrugConcept3.getConceptId() || drugConceptId == $clinDrugConcept4.getConceptId())
                             )                
    then	
        RHSState rhsNN = new RHSState("proton pump inhibitor or misoprostol", "yes", $rhs.getPerson());
        insertLogical(rhsNN);
        
        String s = String.format(
          "Warfarin - NSAID interaction for patient: %s.\n\tClinical implication: Possible increased risk of UGIB or other bleeding\n\tMitigating factor: presence of PPI or misoprostol (%s).\n\tRecommendation: Assess risk and take action if necessary.\n\tExplanation:  Proton pump inhibitors and misoprostol may reduce the risk of UGIB in patients receiving NSAIDs and warfarin.ï€ ", 
          $de1.getPersonId(), 
          $dexp1.getDrugConceptId());
        System.out.println(s);
end

rule "WARFARIN - NSAID -- no PPI or misoprostol - YES AGE"
    when
        $rhs1 : RHSState(stateName == "basic concomitant exposure of warfarin and NSAID" && 
                         state == "yes",
                         $person : person)

        not ($rhs2 : RHSState(stateName == "proton pump inhibitor or misoprostol" && 
                                    state == "yes" && 
                                    person.getPersonId() == $person.getPersonId())
                   )
        
        Person(personId == $person.getPersonId() &&
                yearOfBirth <= 1951)
    then	
        RHSState rhsNN = new RHSState("no PPI or misoprostol - yes age or conditions", "yes", $person);
        insertLogical(rhsNN);
        
        String s = String.format(
            "Warfarin - NSAID interaction - matched 'no PPI or misoprostol - YES AGE' for patient: %s.)", 
            $person.getPersonId());
        System.out.println(s);
end

rule "WARFARIN - NSAID -- no PPI or misoprostol - YES CONDITIONS"
    when
        $rhs1 : RHSState(stateName == "basic concomitant exposure of warfarin and NSAID" && 
                         state == "yes",
                         $person : person)

        not ($rhs2 : RHSState(stateName == "proton pump inhibitor or misoprostol" && 
                                    state == "yes" && 
                                    person.getPersonId() == $person.getPersonId())
                   )
        
        $condConcept : ConceptSetItem(csName == "Upper GI Bleeds" || 
                                      csName == "Peptic Ulcer")
        ConditionEra(personId == $person.getPersonId() && 
                     conditionConceptId == $condConcept.getConceptId())
    then	
        RHSState rhsNN = new RHSState("no PPI or misoprostol - yes age or conditions", "yes", $person);
        insertLogical(rhsNN);
        
        String s = String.format(
            "Warfarin - NSAID interaction - matched 'no PPI or misoprostol - YES CONDITIONS' for patient: %s.)", 
            $person.getPersonId());
        System.out.println(s);
end

rule "WARFARIN - NSAID -- no PPI or misoprostol - NO age or conditions - YES other risk drugs"
    when
        $rhs1 : RHSState(stateName == "basic concomitant exposure of warfarin and NSAID" && 
                         state == "yes",
                         $person : person)

        not (RHSState(stateName == "proton pump inhibitor or misoprostol" && 
                                    state == "yes" && 
                                    person.getPersonId() == $person.getPersonId())
             )
        
        not (RHSState(stateName == "no PPI or misoprostol - yes age or conditions" && 
                         state == "yes" && 
                         person.getPersonId() == $person.getPersonId())
             )
        
        $clinDrugConcept1 : ConceptSetItem(csName == "Systemic Corticosteroids Ingredients" || 
                                      csName == "Aldosterone Antagonists Ingredients")     // TODO: high dose or multiple NSAIDs 
        $de1 : DrugEra(personId == $rhs1.getPerson().getPersonId() && 
                       drugConceptId == $clinDrugConcept1.getConceptId()
                       )
        
        $clinDrugConcept2 : ConceptSetItem(csName == "Systemic Corticosteroids" || 
                                           csName == "Aldosterone Antagonists")
        $dexp1 : DrugExposure(personId == $rhs1.getPerson().getPersonId() && 
                              drugExposureStartDate == $de1.getDrugEraStartDate() &&
                              drugConceptId == $clinDrugConcept2.getConceptId()
                              )        
    then	
        RHSState rhsNN = new RHSState("no PPI or misoprostol - NO age or conditions - YES other risk drugs", "yes", $person);
        insertLogical(rhsNN);
        
        String s = String.format(
            "Warfarin - NSAID interaction for patient: %s.\n\tClinical implication: Increased risk of UGIB or other bleeding\n\tContextual factor: no age or condition risks but risk increasing drug factor (%s).\n\tRecommendation: Use only if benefit outweighs risk.\n\tExplanation: Both corticosteroids and aldosterone antagonists have been shown to substantially increase the risk of UGIB in patients on NSAIDs, with relative risks of 12.8 and 11 respectively compared to a risk of 4.3 with NSAIDs alone (Masclee et al. Gastroenterology 2014;147:784-92.)", 
            $person.getPersonId(), 
            $dexp1.getDrugConceptId());
        System.out.println(s);
end



